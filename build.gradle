plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'org.cyclonedx.bom' version '1.7.4'
	id 'maven-publish'
}

group = 'com.github.akrauze'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17' 
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
		exclude group: 'jakarta.annotation', module: 'jakarta.annotation-api'
	}

	implementation 'org.springframework.boot:spring-boot-starter-log4j2'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.apache.commons:commons-collections4:4.4'
	implementation 'org.apache.commons:commons-lang3'
	implementation 'org.apache.poi:poi:5.2.4'
	implementation('org.apache.poi:poi-ooxml:5.2.4') {
		exclude group: 'com.github.virtuald', module: 'curvesapi'
	}

	implementation 'org.luaj:luaj-jse:3.0.1'

}

def copyReport = tasks.register('copyReport', Copy) {
	dependsOn cyclonedxBom
	from layout.buildDirectory.file("reports/bom.json")
	into layout.buildDirectory.dir("resources/main/META-INF")
}

def copyLicense = tasks.register('copyLicense', Copy) {
	dependsOn copyReport
	from layout.projectDirectory.file("LICENSE")
	into layout.buildDirectory.dir("resources/main/META-INF")
}

jar {
	manifest {
		attributes('SPDX-License-Identifier': 'Apache-2.0')
	}
}

bootJar {
	dependsOn copyLicense
	archiveFileName = "AKESOInventoryExtractorCompanion.jar"
	manifest {
		attributes('SPDX-License-Identifier': 'Apache-2.0')
	}
}

springBoot {
	buildInfo {
		properties {
			additional = ['SPDX-License-Identifier': 'Apache-2.0']
		}
	}
}

cyclonedxBom {
	dependsOn resolveMainClassName
	includeLicenseText = true
	projectType = "application"
}

publishing {
	publications {
		bootJava(MavenPublication) {
			from components.java
			artifact tasks.named("bootJar")
			pom {
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
			}
		}
	}
}

